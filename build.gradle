plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.jetbrains.dokka' version '1.4.10'
}

apply from: './config.gradle'
apply from: './fix.gradle'

// Disable default tasks for root project.
jar.enabled = false
shadowJar.enabled = false
javadoc.enabled = false


// ---------------------------------------------------------------------------------------------------------------------
// Build:
// ---------------------------------------------------------------------------------------------------------------------
def platformBuildApiTasks = []
def platformBuildJarTasks = []
def platformBuildLibs = []

meta.platforms.each {
    platformBuildJarTasks.add(":platform-${it}:build")
    platformBuildApiTasks.add(":platform-${it}:buildApi")
    platformBuildLibs.add(new File(project(":platform-${it}").buildDir, 'libs'))
}

// Task that builds and copies the platform jars.
task buildJars(dependsOn: platformBuildJarTasks, type: Copy) {
    platformBuildLibs.each {
        from it
    }
    
    into new File(buildDir, 'libs')
    
    include '**/*-plugin.jar'
    exclude '**/*-no-runtime.jar'
    rename { String name ->
        return name.replace('-plugin', '')
    }
}

// Task that builds and copies the platform API jars.
task buildApis(dependsOn: platformBuildApiTasks, type: Copy) {
    platformBuildLibs.each {
        from it
    }

    into new File(buildDir, 'libs')

    exclude '**/*-plugin.jar'
    exclude '**/*-no-runtime.jar'
    rename { String name ->
        return name.replace(meta.version, meta.version + "-api");
    }
}

build.dependsOn 'buildJars'
build.dependsOn 'buildApis'

